plugins {
    id 'java'
    id 'maven-publish'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'signing'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

signing {
    sign publishing.publications
}

group 'io.github.alexwatts'
version '1.0.2'

repositories {
    mavenCentral()
    mavenLocal()
}

processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Spread'
                description = 'Spread. Its an ergonomic helper library for building Java Test Objects'
                url = 'https://github.com/alexwatts/spread'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'alexwatts'
                        name = 'Alex Watts'
                    }
                }
                scm {
                    url = 'https://github.com/alexwatts/spread.git'
                    connection = 'scm:git://github.com/alexwatts/spread.gitt'
                    developerConnection = 'scm:git://github.com/alexwatts/spread.git'
                }
            }
        }
    }
}

dependencies {
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation("org.assertj:assertj-core:3.22.0")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

test {
    useJUnitPlatform()
}

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ['com.alwa.spread.*']
    pitestVersion = '1.7.0'
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    verbose = true
}
